apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'com.gasbuddy.mobile:rxsse'

    publishedGroupId = 'com.gasbuddy.mobile'
    artifact = 'rxsse'
    libraryVersion = '0.9'

    libraryName = 'RxSSE'
    libraryDescription = 'A retrofit call adapter that translates server sent events into an RxJava/RxKotlin stream'

    siteUrl = 'https://github.com/gas-buddy/RxSSE'
    gitUrl = 'https://github.com/gas-buddy/RxSSE.git'

    developerId = 'fergusonm'
    developerName = 'Michael Ferguson'
    developerEmail = 'fergusonm@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]

    //Bintray Credentials
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintrayUsername = properties.getProperty('BINTRAY_USERNAME')
    bintrayUserOrg = properties.getProperty('BINTRAY_ORG')
    bintrayApiKey = properties.getProperty('BINTRAY_API_KEY')
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    kotlinOptions {
        jvmTarget = '1.8'
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    api "com.squareup.okio:okio:2.6.0"
    api "com.squareup.okhttp3:okhttp:4.6.0"
    api "com.squareup.retrofit2:retrofit:2.8.1"
    api "io.reactivex.rxjava3:rxjava:3.0.2"
    api "com.github.akarnokd:rxjava3-bridge:3.0.0"
    api "io.reactivex.rxjava3:rxandroid:3.0.0"
}

group = 'com.gasbuddy.mobile'
version = libraryVersion

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    archiveClassifier.set('javadoc')
    from dokka.outputDirectory
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                pom {
                    name = libraryName
                    description = libraryDescription
                    url = siteUrl
                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }
                    scm {
                        url = gitUrl
                    }
                }

                // Applies the component for the release build variant.
                from components.release
                artifact javadocJar
                artifact sourceJar

                groupId = publishedGroupId
                artifactId = artifact
                version = libraryVersion
            }
        }
    }

    bintray {
        user = bintrayUsername
        key = bintrayApiKey

        publications = ['release']
//        configurations = ['archives']
        pkg {
            repo = bintrayRepo
            name = bintrayName
            userOrg = bintrayUserOrg
            desc = libraryDescription
            websiteUrl = siteUrl
            vcsUrl = gitUrl
            licenses = allLicenses
            dryRun = false
            publish = true
            version {
                desc = libraryDescription
            }
        }

        // publish = true // when jcenter link is set up
    }
}